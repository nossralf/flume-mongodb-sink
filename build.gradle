plugins {
    id 'jacoco'
    id 'java-library'
    id 'org.sonarqube' version '3.4.0.2513'
}

repositories {
    mavenCentral()
}

def flumeVersion = '1.10.1'

dependencies {
    api "org.apache.flume:flume-ng-configuration:${flumeVersion}"
    api "org.apache.flume:flume-ng-core:${flumeVersion}"
    api "org.apache.flume:flume-ng-sdk:${flumeVersion}"

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.mongodb:mongodb-driver:3.12.11'
    implementation 'org.slf4j:slf4j-api:2.0.3'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.testcontainers:testcontainers:1.17.4'
}

group = 'art.iculate.flume'
version = '0.2'
description = 'flume-mongodb-sink'
sourceCompatibility = '1.8'

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    dependsOn 'test'
    reports {
        xml.required = true
    }
}

check.dependsOn jacocoTestReport

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'nossralf'
        property 'sonar.projectKey', 'nossralf_flume-mongodb-sink'
  }
}

tasks['sonarqube'].dependsOn check

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
}
